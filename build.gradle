plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'scala'
    id 'idea'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.7.1'
//    id "com.github.lkishalmi.gatling" version "3.0.0"
}

spotbugsMain {
    showStackTraces = true
    extraArgs.set([ '-longBugCodes' ])
    reports {
        text {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.text")
        }
        xml {
            enabled = false
        }
    }
}

task spotbugsReport {
    doLast {
        println("Spotbugs report:")
        file("$buildDir/reports/spotbugs/main/spotbugs.text").readLines().forEach {
            println( it )
        }
    }
}
spotbugsMain.finalizedBy spotbugsReport

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

apply from: 'coverage.gradle'

group = 'ru.eda'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.h2database:h2:1.4.200'

    implementation 'io.springfox:springfox-swagger2:2.9.2',
                    'io.springfox:springfox-swagger-ui:2.9.2'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

//gatling {
//    toolVersion = '3.0.0-RC4'
//    logLevel = 'INFO'
//    jvmArgs = ['-server', '-Xms512M', '-Xmx512M']
//}